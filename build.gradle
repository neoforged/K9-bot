plugins {
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id 'org.ajoberstar.grgit' version '5.0.0'
}

apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

archivesBaseName = "K9"
group = 'com.tterrag.k9'
description = "Discord bot with MCP lookups and more"
sourceCompatibility = 1.8
targetCompatibility = 1.8

def repo = grgit.open(dir: project.projectDir)

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else {
    version += ".${repo.log().find().abbreviatedId}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
	outputs.upToDateWhen { false }
	doLast {
		file("${sourceSets.main.output.resourcesDir}/mcdrama/.dramaversion").text = 'git submodule --quiet foreach git rev-parse --short HEAD'.execute().in.text.trim()
	}
}

jar {
	from('.') {
		include 'mcdrama/**' 
	}
	manifest {
 	   attributes 'Main-Class': 'com.tterrag.k9.K9',
 	              'Implementation-Version': project.version
	}
}

shadowJar {
	configurations = [project.configurations.runtimeClasspath] 
	mergeServiceFiles()
	dependencies {
		exclude "*.html"
		exclude dependency('org.projectlombok:lombok')
	}
    archiveFile = file("$buildDir/libs/k9-all.jar")
}

repositories {
    mavenCentral()
	jcenter()
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

dependencies {
    implementation 'com.discord4j:discord4j-core:3.3.0-SNAPSHOT'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.0'
    implementation 'org.pircbotx:pircbotx:2.3-SNAPSHOT'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.guava:guava:27.1-jre'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.0'
    implementation 'it.unimi.dsi:fastutil:8.2.2'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation "com.beust:jcommander:1.72"
    implementation 'net.jodah:typetools:0.6.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.jruby:jruby-complete:9.2.7.0'
    implementation 'org.clojure:clojure:1.7.0'
    implementation 'org.clojure:math.numeric-tower:0.0.4'
    implementation fileTree(dir: 'libs', include:'*.jar')
    
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

tasks.register('outputVersion') {
    doLast {
        file(System.getenv('GITHUB_ENV')) << "\nBOT_VERSION=${project.version}"
    }
}